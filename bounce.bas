1 REM BOUNCE -- PLOT OF BOUNCING BALL
2 REM
3 REM FROM 101 BASIC COMPUTER GAMES
4 REM DIGITAL EQUIPMENT CORPORATION
5 REM MAYNARD, MASSACHUSETTS 01754
6 REM
7 REM 1975
8 REM
9 REM DESCRIPTION
10 REM
11 REM THIS PROGRAM PLOTS A BOUNCING BALL.  MOST COMPUTER PLOTS RUN
12 REM ALONG THE PAPER IN THE TERMINAL (TOP TO BOTTOM); HOWEVER, THIS
13 REM PLOT IS DRAWN HORIZONTALLY ON THE PAPER (LEFT TO RIGHT).
14 REM
15 REM YOU MAY SPECIFY THE INITIAL VELOCITY OF THE BALL AND THE 
16 REM COEFFICIENT OF ELASTICITY OF THE BALL (A SUPERBALL IS ABOUT
17 REM 0.85 -- OTHER BALLS ARE MUCH LESS).  YOU ALSO SPECIFY THE
18 REM TIME INCREMENT TO BE USED IN "STROBING" THE FLIGHT OF THE
19 REM BALL.  IN OTHER WORDS, IT IS AS THOUGH THE BALL IS THROWN UP
20 REM IN A DARKENED ROOM AND YOU FLASH A LIGHT AT FIXED TIME INTERVALS
21 REM AND PHOTOGRAPH THE PROGRESS OF THE BALL.
22 REM
23 REM PROGRAM AUTHOR:
24 REM
25 REM VAL SKALABRIN
26 REM NEWPORT-MESA UNIFIED SCHOOL DISTRICT
27 REM NEWPORT BEACH, CA 92660
28 REM
90 DIM T(20)
100 PRINT "THIS SIMULATION LETS YOU SPECIFY THE INITIAL VELOCITY"
110 PRINT "OF A BALL THROWN STRAIGHT UP, AND THE COEFFICIENT OF"
120 PRINT "ELASTICITY OF THE BALL.  PLEASE USE A DECIMAL FRACTION"
130 PRINT "COEFFICIENT (LESS THAN 1)."
131 PRINT
132 PRINT "YOU ALSO SPECIFY THE TIME INCREMENT TO BE USED IN"
133 PRINT "STROBING THE BALL'S FLIGHT (TRY 0.1 INITIALLY)."
134 PRINT
135 PRINT "TIME INCREMENT (SEC)";
136 INPUT S2
137 IF S2=0 THEN 430
140 PRINT
150 PRINT "VELOCITY (FPS)";
160 INPUT V
165 PRINT
170 PRINT "BOUNCE COEFFICIENT";
180 INPUT C
184 PRINT
185 PRINT "FEET"
186 PRINT
187 S1=INT(70/(V/(16*S2)))
190 FOR I=1 TO S1
200 T(I)=V*C^(I-1)/16
210 NEXT I
220 FOR H=INT(-16*(V/32)^2+V^2/32+0.5) TO 0 STEP -0.5
221 IF INT(H)<>H THEN 225
222 PRINT H;
225 L=0
230 FOR I=1 TO S1
240 FOR T=0 TO T(I) STEP S2
245 L=L+S2
250 IF ABS(H-(0.5*(-32)*T^2+V*C^(I-1)*T))>0.25 THEN 270
260 PRINT TAB(L/S2);"O";
270 NEXT T
275 T=T(I+1)/2
276 IF -16*T^2+V*C^(I-1)*T<H THEN 290
280 NEXT I
290 PRINT
300 NEXT H
310 PRINT TAB(1);
320 FOR I=1 TO INT(L+1)/S2+1
330 PRINT ".";
340 NEXT I
350 PRINT
355 PRINT " 0";
360 FOR I=1 TO INT(L+0.9995)
380 PRINT TAB(INT(I/S2));I;
390 NEXT I
400 PRINT
410 PRINT TAB(INT(L+1)/(2*S2)-2);"SECONDS"
420 PRINT
430 END

